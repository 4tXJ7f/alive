Name: extCmpExt:Compares:4095
%a = fpext %x
%b = fpext %y
%r = fcmp %a, %b
  =>
%r = fcmp %x, %y

; XXX: Do more
Name: negxOEQC:Compares:4031
%a = fsub -0.0, %x
%r = fcmp oeq %a, C
  =>
%r = fcmp oeq %x, -C


Name: xUNOx:Compares:3938
%r = fcmp uno %x, %x
  =>
%r = fcmp uno %x, 0.0


Name: xULTx:Compares:3939
%r = fcmp ult %x, %x
  =>
%r = fcmp uno %x, 0.0


Name: xUGTx:Compares:3940
%r = fcmp ugt %x, %x
  =>
%r = fcmp uno %x, 0.0


Name: xUNEx:Compares:3941
%r = fcmp une %x, %x
  =>
%r = fcmp uno %x, 0.0


Name: xORDx:Compares:3947
%r = fcmp ord %x, %x
  =>
%r = fcmp ord %x, 0.0


Name: xORDx:Compares:3948
%r = fcmp oeq %x, %x
  =>
%r = fcmp ord %x, 0.0


Name: xORDx:Compares:3949
%r = fcmp oge %x, %x
  =>
%r = fcmp ord %x, 0.0


Name: xORDx:Compares:3950
%r = fcmp ole %x, %x
  =>
%r = fcmp ord %x, 0.0


; Ignore symmetric case
Name: NegMulNeg:MulDivRem:654-1
%a = fsub -0.0, %x
%b = fsub -0.0, %y
%r = fmul %a, %b
  =>
%r = fmul %x, %y


; Ignore symmetric case
Name: NegMulNeg:MulDivRem:660-1
Pre: hasOneUse(%a)
%a = fsub -0.0, %x
%r = fmul %a, %y
  =>
%b = fmul %x, %y
%r = fsub -0.0, %b


Name: SubNegToPlus:AddSub:1749
%a = fsub -0.0, %y
%b = fptrunc %a
%r = fsub %x, %b
  =>
%c = fptrunc %y
%r = fadd %x, %c


Name: SelectZero:AddSub:1399-1
%a = select %c, -0.0, C1
%b = select %c, C2, -0.0
%r = fadd %a, %b
  =>
%r = select %c, C2, C1


Name: SelectZero:AddSub:1402-1
%a = select %c, C1, -0.0
%b = select %c, -0.0, C2
%r = fadd %a, %b
  =>
%r = select %c, C1, C2


Name: FAddToAdd:AddSub:1374
Pre: hasOneUse(%a) && hasOneUse(%b) && WillNotOverflowSignedAdd(%x, %y)
%a = sitofp %x
%b = sitofp %y
%r = fadd %a, %b
  =>
%c = add nsw %x, %y
%r = sitofp %c


Name: FAddToAdd:AddSub:1360
Pre: hasOneUse(%a) && WillNotOverflowSignedAdd(%x, fptosi(C)) && (sitofp(%conv) == C)
%a = sitofp %x
%r = fadd %a, C
  =>
%conv = fptosi(C)
%b = add nsw %x, %conv
%r = sitofp %b


Name: AddZero:InstructionSimplify:806-1
Pre: CannotBeNegativeZero(%x)
%r = fadd %x, 0.0
  =>
%r = %x


Name: SubZero:InstructionSimplify:845-1
Pre: CannotBeNegativeZero(%x)
%r = fsub %x, -0.0
  =>
%r = %x


Name: MulOne:InstructionSimplify:882
%r = fmul %x, 1.0
  =>
%r = %x


Name: AddZero:InstructionSimplify:802
%r = fadd %x, -0.0
  =>
%r = %x


Name: SubZero:InstructionSimplify:841
%r = fsub %x, 0.0
  =>
%r = %x


Name: ZeroSubZeroSub:InstructionSimplify:852-1
%a = fsub -0.0, %x
%r = fsub -0.0, %a
  =>
%r = %x


Name: SubNegToPlus:AddSub:1756
%a = fsub -0.0, %y
%b = fpext %a
%r = fsub %x, %b
  =>
%c = fpext %y
%r = fadd %x, %c


Name: MulNegOne:MulDivRem:555
%r = fmul %X, -1.0
  =>
%r = fsub -0.0, %X


Name: SubNegToPlus:AddSub:1744
%a = fsub -0.0, %y
%r = fsub %x, %a
  =>
%r = fadd %x, %y


Name: addToSubRev
%a = fsub -0.0, %X
%r = fadd %a, %Y
  =>
%r = fsub %Y, %X
